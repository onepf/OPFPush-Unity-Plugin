apply plugin: 'java'

dependencies {
    runtime(project(':android')) {
        transitive = false
    }

    runtime('org.onepf:opfutils:0.1.24') {
        transitive false
    }
    runtime('org.onepf:opfpush:0.3.1') {
        transitive false
    }
    runtime('org.onepf:opfpush-gcm:0.3.1') {
        transitive false
    }
    runtime('org.onepf:opfpush-adm:0.3.1') {
        transitive false
    }
    runtime('org.onepf:opfpush-nokia:0.3.1') {
        transitive false
    }
    runtime('com.nokia:push:1.0') {
        transitive false
    }
}

// Build unity package
def sep = File.separator
def unitySrcPath = "${rootProject.projectDir.absolutePath}${sep}opfpush-unity${sep}unity${sep}Assets${sep}Plugins${sep}Android"
def manifestPath = "${project.projectDir.absolutePath}${sep}Assets${sep}Manifests"
def allProvidersManifestName = "AllProvidersAndroidManifest.xml"
def withoutNokiaManifestName = "WithoutNokiaAndroidManifest.xml"

clean {
    doLast {
        project.buildDir.deleteDir()

        //delete old dependencies
        file(unitySrcPath).eachFileMatch(~/((?!android-support-v4.jar|google-play-services.jar).).*/) { file ->
            println 'delete ' + file
            file.delete()
        }
    }
}

build {
    doLast {
        def unityPackageAbsolutePath = "${project.buildDir.absolutePath}${sep}outputs"
        delete fileTree(dir: unityPackageAbsolutePath, include: '*.unitypackage')
        mkdir unityPackageAbsolutePath

        def nokiaDependenciesPattern = ~/(push-1.0.jar)|(.*nokia.*)/
        //get dependencies jars without nokia dependencies
        configurations.runtime.resolve()
                .findAll { file -> !file.getName().matches(nokiaDependenciesPattern) }
                .each {
            println it
            def dependency = it
            copy {
                from dependency.getParent()
                into unitySrcPath
                include dependency.getName()
            }
        }

        //get opfpush-unity.jar
        def tmpDirPath = "${unitySrcPath}${sep}tmp"

        //move android project archive to tmp dir for unzip
        mkdir tmpDirPath
        file("${unitySrcPath}${sep}android-release.aar").renameTo(file("${tmpDirPath}${sep}android-release.zip"))
        ant.unzip(src: "${tmpDirPath}${sep}android-release.zip", dest: tmpDirPath)

        //copy classes.jar to Android src dir with renaming
        copy {
            from tmpDirPath
            into unitySrcPath
            include 'classes.jar'
        }
        file("${unitySrcPath}${sep}classes.jar").renameTo "${unitySrcPath}${sep}opfpush-unity-${VERSION_NAME}.jar"

        //Remove tmp dir
        file(tmpDirPath).deleteDir()

        println "Unity package build: $unityPackageAbsolutePath"

        //copy android manifest without nokia pushes.
        copy {
            from manifestPath
            into unitySrcPath
            include withoutNokiaManifestName
        }
        file("${unitySrcPath}${sep}${withoutNokiaManifestName}").renameTo "${unitySrcPath}${sep}AndroidManifest.xml"

        //Build unitypackage without nokia provider
        buildUnityPackage("${unityPackageAbsolutePath}${sep}opfpush-gcm-adm-${VERSION_NAME}.unitypackage", unityPackageAbsolutePath)
        file("${unitySrcPath}${sep}AndroidManifest.xml").delete()

        //get nokia dependencies jars
        configurations.runtime.resolve()
                .findAll { file -> file.getName().matches(nokiaDependenciesPattern) }
                .each {
            println it
            def dependency = it
            copy {
                from dependency.getParent()
                into unitySrcPath
                include dependency.getName()
            }
        }
        file("${unitySrcPath}${sep}push-1.0.jar").renameTo "${unitySrcPath}${sep}nokia-push-1.0.jar"

        //copy android manifest with all providers
        copy {
            from manifestPath
            into unitySrcPath
            include allProvidersManifestName
        }
        file("${unitySrcPath}${sep}${allProvidersManifestName}").renameTo "${unitySrcPath}${sep}AndroidManifest.xml"

        //Build unitypackage with all providers
        buildUnityPackage("${unityPackageAbsolutePath}${sep}opfpush-all-${VERSION_NAME}.unitypackage", unityPackageAbsolutePath)

        println "Build log: ${unityPackageAbsolutePath}${sep}unity-plugin-build.log"
    }
}

def getUnityExecutablePath() {
    if (project.hasProperty('unityExecutable')) {
        return project.property('unityExecutable');
    } else {
        def paths = ['unityMacPath', 'unityWin86Path', 'unityWin64Path',]
        for (path in paths) {
            if (file(project.property(path)).exists()) {
                return project.property(path);
            }
        }
    }

    throw new RuntimeException("Unity not found. Please make sure Unity " +
            "is installed at default location or provide 'unityExecutable' property.");
}

def buildUnityPackage(packageName, unityPackageAbsolutePath) {
    def sep = File.separator
    def unityPath = getUnityExecutablePath()
    def unityRootAbsolutePath = "${rootProject.projectDir.absolutePath}${sep}opfpush-unity${sep}unity"
    println "Unity executable: $unityPath"
    println "Unity package source root: $unityRootAbsolutePath"

    project.exec {
        commandLine unityPath
        args '-batchmode'
        args '-projectPath'
        args "${unityRootAbsolutePath}"
        args '-exportPackage'
        args "Assets${sep}Plugins"
        args packageName
        args '-logFile'
        args "${unityPackageAbsolutePath}${sep}unity-plugin-build.log"
        args '-quit'
    }
}